#!/usr/bin/env bash

# The contents of the log file are given in stdin.
cat /dev/stdin | awk '
function alen(a,  i,j) {
  j = 0
  for( i in a) {
    j++
  }
  return j
}

function print_array(arr, title,   i) {
  if( alen(arr) > 0 ) {
    printf("%s\n======================\n", title)
    for(i=0;i<alen(arr);++i) {
      printf(" %s\n", arr[i+1])
    }
    printf("\n")
  }
}

BEGIN {
  # Fields are separated via \t
  FS = "\t"

  # count transactions
  transactions = 0

  # set variable for storing tid
  last_tid = ""
}

# record warnings and errors
{
  string = $1 "   " $4
  if( $3 == "warn" ) {
    warnings[alen(warnings)+1] = string
  } else if( $3 == "die" ) {
    errors[alen(errors)+1] = string
  }
}

# record created repos
{
  if( $3 == "create" ) {
    created_repos[alen(created_repos)+1] = $5 " created " $4 " (" $6 ")"
  }
}

# count tids
{
  if( $2 != last_tid ) {
    ++transactions;
    last_tid=$2
  }
}
# record transaction ips
{
  if( $3 == "ssh" || $3 == "http") {
    ips[$6]++
  }
}

# print results
END {
  print_array(errors, " !      ERRORS      ! ")
  print_array(warnings, "       warnings")
  print_array(created_repos, "    created repos")

  printf("     transactions\n======================\n")
  for(ip in ips) {
    printf(" %-5s%s\n", ips[ip], substr(ip, 6))
  }
  printf("----------------------\n %-5d%s\n", transactions, "total")
}
'
